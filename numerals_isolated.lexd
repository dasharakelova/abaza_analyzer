PATTERN Num11-19
NumRoot10 Link[link_add] (NumRoot1|NumRoot2|NumRoot3|NumRoot4|NumRoot5-9)[link_add] # 11-19
NumRoot10 Link[link_shwa] (NumRoot1|NumRoot2|NumRoot3|NumRoot4|NumRoot5-9)[link_shwa] # 11-19
NumRoot10 Link[link_e] (NumRoot1|NumRoot2|NumRoot3|NumRoot4|NumRoot5-9)[link_e] # 11-19
NumRoot10 Link[no_link] (NumRoot1|NumRoot2|NumRoot3|NumRoot4|NumRoot5-9)[no_link] # 11-19

PATTERNS
NumRoot1 OneSuf1 # 1
NumRoot2 OneSuf2 # 2
(NumRoot3|NumRoot4|NumRoot5-9|NumRoot10)[3] OneSuf3-10[3] # 3
(NumRoot3|NumRoot4|NumRoot5-9|NumRoot10)[-3] OneSuf3-10[-3] # 4-10

Num11-19
((NumRoot2|NumRoot3|NumRoot4) ӏter)? Twenty # 20, 40, 60, 80
NumRoot200-600 OneSuf100 # 100
(NumRoot2|NumRoot3|NumRoot4|NumRoot5-9[|[5,6]]) NumRoot200-600 # 200-900 # phon: шв > ышв/вы
NumRoot5-9[|[7,8,9]]                            NumRoot700-900 # 200-900 # phon: шв > ышв/вы
((NumRoot2|NumRoot3|NumRoot4|NumRoot5-9|NumRoot10) ӏter)? Thousand # 1000-10000
Num11-19 ӏter Thousand # 11000-19000


LEXICON NumRoot1
зы<num>:зы[link_add]

LEXICON NumRoot2
гӏвы<num>:гӏвы{R}[link_shwa]

LEXICON NumRoot3
хы<num>:хы{R}[no_link,3]

LEXICON NumRoot4
пщы<num>:пщы{R}[link_add]

LEXICON NumRoot5-9
хвы<num>:хвы{R}[link_shwa,5]
цы<num>:цы{R}[link_add,6]
быжь<num>:б{ы}жьы{R}[link_add,7]
агӏы<num>:а{R}гӏ[no_link,8]
жвы<num>:жвы{R}[link_e,9]

LEXICON NumRoot10
жва<num>:жва{R}

LEXICON NumRoot200-600
швы<num>:швы{R}

LEXICON NumRoot700-900
швы<num>:швы{D}

LEXICON Twenty
гӏважва<num>:гӏважва

LEXICON Thousand
зыкь<num>:з{ы}кьы{R} # phon: зыкь > зкьы

LEXICON ӏter
<iter>:н

LEXICON Link
<link>:и{R}[link_add]
<link>:ы{R}[link_shwa]
<link>:е[link_e]
<link>:[no_link]

LEXICON OneSuf1
<n>:акӏы{D}
<h>:аджвы{D}

LEXICON OneSuf2
<n>:ба{D}
<h>:джь

LEXICON OneSuf3-10
<n>:ба{D}
<n>:па{D}[3]
<h>:гӏвы{D}[3]
<h>:гӏвы{D}

LEXICON OneSuf100
<n>:кӏы{D}
<h>:гӏвы{D}
